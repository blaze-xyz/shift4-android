plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'signing'
}

android {
    namespace 'com.shift4'

    defaultConfig {
        aarMetadata {
            minCompileSdk = 21
        }
        compileSdk 33
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 4
        versionName rootProject.version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
            ignoreFailures = true
            reports {
//                junitXml.enabled = true
            }
        }
        unitTests.includeAndroidResources = true
    }

    buildTypes {
        debug {
            buildConfigField("long", "VERSION_CODE", "${defaultConfig.versionCode}")
            buildConfigField("String", "VERSION_NAME", "\"${defaultConfig.versionName}\"")
            buildConfigField("String", "API_URL", "\"https://api.shift4.com\"")
            buildConfigField("String", "BACKOFFICE_URL", "\"https://dev.shift4.com\"")

            minifyEnabled false
            shrinkResources false
            consumerProguardFiles 'consumer-rules.pro'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            buildConfigField("long", "VERSION_CODE", "${defaultConfig.versionCode}")
            buildConfigField("String", "VERSION_NAME", "\"${defaultConfig.versionName}\"")
            buildConfigField("String", "API_URL", "\"https://api.shift4.com\"")
            buildConfigField("String", "BACKOFFICE_URL", "\"https://dev.shift4.com\"")

            minifyEnabled false
            shrinkResources false
            consumerProguardFiles 'consumer-rules.pro'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    kotlin {
        jvmToolchain {
            languageVersion.set(JavaLanguageVersion.of("11"))
        }
    }
    apply plugin: 'maven-publish'
    tasks.withType(Test) {
        maxParallelForks = Runtime.runtime.availableProcessors()
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }

    dependencies {
        implementation 'com.shift4:shift4-android-3ds:1.0.0'

        //Android
        implementation 'androidx.core:core-ktx:1.6.0'
        implementation 'com.google.android.material:material:1.9.0'
        implementation "androidx.fragment:fragment-ktx:1.5.7"

        implementation 'com.nimbusds:nimbus-jose-jwt:9.13'
        implementation 'com.google.code.gson:gson:2.10.1'
        implementation 'com.github.kittinunf.fuel:fuel:2.3.1'
        implementation 'com.github.kittinunf.fuel:fuel-gson:2.3.1'
        implementation 'com.github.kittinunf.fuel:fuel-coroutines:2.3.1'
        implementation 'com.github.bumptech.glide:glide:4.15.1'

        //Encryption
        implementation 'com.nimbusds:nimbus-jose-jwt:9.13'
        implementation 'org.bouncycastle:bcprov-jdk15on:1.69'
        implementation 'org.bouncycastle:bcpkix-jdk15on:1.69'
        implementation 'com.scottyab:rootbeer-lib:0.1.0'

        //Retrofit
        implementation 'com.squareup.okhttp3:okhttp:4.11.0'
        implementation 'com.squareup.retrofit2:retrofit:2.9.0'
        implementation 'com.squareup.retrofit2:converter-gson:2.5.0'

        //Gson
        implementation 'com.google.code.gson:gson:2.10.1'

        implementation 'com.github.bumptech.glide:glide:4.15.1'

        //Coroutines
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.5'

        //External
        implementation 'com.alimuzaffar.lib:pinentryedittext:2.0.6'
        implementation 'com.hbb20:ccp:2.7.3'
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.shift4'
            artifactId = 'shift4-android'
            version = rootProject.version

            pom {
                name = 'Shift4 Android SDK'
                description = 'Android client library for Shift4'
                url = 'https://dev.shift4.com/'
                licenses {
                    license {
                        name = 'MIT license'
                        url = 'http://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        name = 'Shift4'
                        email = 'devsupport@shift4.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:shift4developer/shift4-android.git'
                    developerConnection = 'scm:git:git@github.com:shift4developer/shift4-android.git'
                    url = 'https://github.com/shift4developer/shift4-android.git'
                }
            }

            afterEvaluate {
                from components.release
            }
        }

    }
    repositories {
        maven {
            name = 'myrepo'
            url = "${project.buildDir}/repo"
        }
        maven {
            credentials {
                username = findProperty('deploy.ossrhUsername')
                password = findProperty('deploy.ossrhPassword')
            }

            url 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
        }
    }
}

signing {
    def signingKey = findProperty("signing.signingKey")
    def signingPassword = findProperty("signing.signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)

    sign publishing.publications.release
}


if (project.file("src/test/dependencies.gradle").exists()) {
    apply from: 'src/test/dependencies.gradle'
}